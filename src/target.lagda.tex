\documentclass{article}

\usepackage[active,tightpage]{preview}
\PreviewEnvironment{prev}
\setlength\PreviewBorder{0pt}

\usepackage{agda}
\usepackage{amsmath}
\usepackage{amssymb}
\usepackage{mathabx}

\usepackage{unicode-math}
\setmathfont{XITS Math}

\usepackage{newunicodechar}
\newunicodechar{≤}{\ensuremath{\leq}}
\newunicodechar{≡}{\ensuremath{\equiv}}
\newunicodechar{∸}{\ensuremath{\dotdiv}}
\newunicodechar{∀}{\ensuremath{\forall}}
\newunicodechar{→}{\ensuremath{\rightarrow}}
\newunicodechar{ℕ}{\ensuremath{\mathbb{N}}}
\newunicodechar{ℤ}{\ensuremath{\mathbb{Z}}}
\newunicodechar{δ}{\ensuremath{\delta}}
\newunicodechar{∈}{\ensuremath{\in}}
\newunicodechar{⊢}{\ensuremath{\vdash}}
\newunicodechar{⇒}{\ensuremath{\Rightarrow}}
\newunicodechar{⟶}{\ensuremath{\longrightarrow}}
\newunicodechar{ₛ}{\ensuremath{\textsubscript{s}}}
\newunicodechar{ᵛ}{\ensuremath{\textsuperscript{v}}}

\begin{document}
\begin{prev}
\begin{code}
module target where

-- Operator precedence and associativity
infix 4 _≤ₛ_
infixl 6 _∸ₛ_ _-ₛ_

open import lib

-- Stack descriptor: (frames, displacement)
record SD : Set where
    constructor ⟨_,_⟩
    field
        f : ℕ
        d : ℕ

-- Stack descriptor operations    
_+ₛ_ : SD → ℕ → SD
⟨ f , d ⟩ +ₛ n = ⟨ f , d + n ⟩

_∸ₛ_ : SD → ℕ → SD
⟨ f , d ⟩ ∸ₛ n = ⟨ f , d ∸ n ⟩

-- _-ₛ_ : (sd : SD) → (n : ℕ) → n ≤ SD.d sd → SD
-- (⟨ S_f , S_d ⟩ -ₛ n) p = ⟨ S_f , (S_d - n) p ⟩

_-ₛ_ : (sd : SD) → (n : ℕ) → (n≤d : n ≤ SD.d sd) → SD
(⟨ f , d ⟩ -ₛ n) n≤d = ⟨ f , (d - n) n≤d ⟩

-ₛ≡ : ∀ {f d d' n} → {n≤d' : n ≤ d'} → (d' - n) n≤d' ≡ d → ⟨ f , d ⟩ ≡ (⟨ f , d' ⟩ -ₛ n) n≤d'
-ₛ≡ p rewrite p = refl

-- Stack descriptor lexicographic ordering
data _≤ₛ_ : SD → SD → Set where
    <-f : ∀ {f f' d d'} → f < f' → ⟨ f , d ⟩ ≤ₛ ⟨ f' , d' ⟩
    ≤-d : ∀ {f d d'} → d ≤ d' → ⟨ f , d ⟩ ≤ₛ ⟨ f , d' ⟩

≤ₛ-refl : ∀{sd : SD} → sd ≤ₛ sd
≤ₛ-refl {⟨ f , d ⟩} = ≤-d ≤-refl

≤ₛ-trans : ∀{sd sd' sd'' : SD} → sd ≤ₛ sd' → sd' ≤ₛ sd'' → sd ≤ₛ sd''
≤ₛ-trans (<-f f<f') (≤-d _) =  <-f f<f'
≤ₛ-trans (<-f f<f') (<-f f'<f'') = <-f (<-trans f<f' f'<f'')
≤ₛ-trans (≤-d _) (<-f f'<f'') = <-f f'<f''
≤ₛ-trans (≤-d d≤d') (≤-d d'≤d'') = ≤-d (≤-trans d≤d' d'≤d'')

+ₛ→≤ₛ : ∀{sd : SD} → ∀{n : ℕ} → sd ≤ₛ sd +ₛ n
+ₛ→≤ₛ = ≤-d +→≤ 

-- Operator
data UnaryOp : Set where 
    UNeg : UnaryOp

data BinaryOp : Set where
    BPlus : BinaryOp
    BMinus : BinaryOp
    BTimes : BinaryOp

data RelOp : Set where
    RLeq : RelOp
    RLt : RelOp

-- Nonterminals
-- Lefthand sides
data L (sd : SD) : Set where
    l-var : (sdᵛ : SD) → sdᵛ ≤ₛ sd → L sd
    l-sbrs : L sd

-- Simple righthand sides
data S (sd : SD) : Set where
    s-l : L sd → S sd
    s-lit : ℤ → S sd

-- Righthand sides
data R (sd : SD) : Set where
    r-s : S sd → R sd
    r-unary : UnaryOp → S sd → R sd
    r-binary : S sd → BinaryOp → S sd → R sd

-- Instruction sequences
data I (sd : SD) : Set where
    stop : I sd
    assign-inc : (δ : ℕ) → L (sd +ₛ δ) → R sd → I (sd +ₛ δ) → I sd
    assign-dec : (δ : ℕ) → (δ≤d : δ ≤ SD.d sd) → L ((sd -ₛ δ) δ≤d) → R sd → I ((sd -ₛ δ) δ≤d)  → I sd
    if-then-else-inc : (δ : ℕ) → S sd → RelOp → S sd → I (sd +ₛ δ) → I (sd +ₛ δ) → I sd
    if-then-else-dec : (δ : ℕ) → (δ≤d : δ ≤ SD.d sd) → S sd → RelOp → S sd → I ((sd -ₛ δ) δ≤d) → I ((sd -ₛ δ) δ≤d) → I sd
    adjustdisp-inc : (δ : ℕ) → I (sd +ₛ δ) → I sd
    adjustdisp-dec : (δ : ℕ) → (δ≤d : δ ≤ SD.d sd) → I ((sd -ₛ δ) δ≤d) → I sd
    popto : (sd' : SD) → sd' ≤ₛ sd → I sd' → I sd 
\end{code}
\end{prev}
\end{document}
